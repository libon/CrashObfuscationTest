apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

// Apply the Crashlytics Gradle plugin
apply plugin: 'com.google.firebase.crashlytics'

ext.linphoneVersion = "4.4.1-pre.30+0197fa6"
android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        // Make sure the applicationId matches the one in app/src/release/google-services.json
        applicationId "com.example.crashobfuscationtest"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            // Specify a different applicationId or a suffix
            // so that the applicationId matches the one in app/src/debug/google-services.json
            // applicationIdSuffix ".test"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            def signingPropertiesFile = project.rootProject.file("signing.properties")
            if (signingPropertiesFile.exists()) {
                def properties = new Properties()
                properties.load(project.rootProject.file('signing.properties').newDataInputStream())
                println "Using signing properties from signing.properties"
                signingConfigs {
                    release {
                        keyAlias properties.getProperty("AndroidSigningKeyAlias")
                        keyPassword properties.getProperty("AndroidSigningKeyPassword")
                        storeFile file(properties.getProperty("AndroidSigningStoreFile"))
                        storePassword properties.getProperty("AndroidSigningStorePassword")
                    }
                    android.buildTypes.release.signingConfig = signingConfigs.release
                }
            }
        }
    }

    def ndkProperties = new Properties()
    ndkProperties.load(new FileInputStream("${System.env.ANDROID_HOME}/ndk-bundle/source.properties"))
    ndkVersion = ndkProperties.get("Pkg.Revision")
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    implementation  "org.linphone.minimal:linphone-sdk-android:$linphoneVersion"
    implementation 'com.google.firebase:firebase-crashlytics-ndk:17.3.0'

}

repositories {
    maven {
        url "https://linphone.org/maven_repository/"
    }
}

apply from: file('crashlytics_ndk_symbols.gradle')
apply plugin: 'com.google.gms.google-services'
